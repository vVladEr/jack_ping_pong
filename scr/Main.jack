class Main {
    function void main () {
        var bool endGame;
        var Bat playerBat, aiBat;
        var Ball ball;
        var char key, aiKey;
        var Vector2 dir;
        var int delta;
        var int yExp;
        var int yDir;
        var int prevBallX, prevBallY, prevBallRadius;
        var int prevBatXLT, prevBatYLT, prevBatXRB, prevBatYRB;

        let endGame = false;
        let playerBat = Bat.new(0,5);
        let aiBat = Bat.new(502,3);
        let ball = Ball.new();

        do ball.draw();
        do aiBat.draw();
        do playerBat.draw();

        while (endGame = false)
        {
            let key = Keyboard.keyPressed();
            if(~(key = 0))
            {
                let prevBatXLT = playerBat.getXLT();
                let prevBatYLT = playerBat.getYLT();
                let prevBatXRB = playerBat.getXRB();
                let prevBatYRB = playerBat.getYRB();
                do playerBat.changePosition(key);

                do Screen.setColor(false);
                do Screen.drawRectangle(prevBatXLT, prevBatYLT, prevBatXRB, prevBatYRB);

                do playerBat.draw();
            }

            let prevBallX = ball.getX();
            let prevBallY = ball.getY();
            let prevBallRadius = ball.getRadius();

            let dir = ball.getDirection();

            let yDir = dir.getY();
            if ((ball.getX() > playerBat.getXRB()) & 
                ((ball.getX() - ball.getRadius() + dir.getX()) < playerBat.getXRB())){
                let delta = ball.getX()-ball.getRadius()-playerBat.getXRB();
                let yDir = yDir * delta / dir.getX();
                if (yDir < 0){
                    if(-yDir > (ball.getY() - ball.getRadius())){
                        let yExp = 2 * ball.getRadius() - ball.getY() - yDir;
                    }
                    else{
                        let yExp = ball.getY() + yDir;
                    }
                }
                else{
                    if (yDir > (255 - ball.getY() - ball.getRadius())){
                        let yExp = - 2 * ball.getRadius() + 510 - yDir - ball.getY();
                    }
                    else{
                        let yExp = ball.getY() + yDir;
                    }
                }
                if ((yExp + ball.getRadius() > playerBat.getYLT())&
                    (yExp - ball.getRadius() < playerBat.getYRB())){
                    do ball.hitBat(playerBat, key);
                }
                else{
                    let endGame = true;
                }
            }
            else {
                if ((ball.getX() < aiBat.getXLT()) & 
                    (ball.getX() + ball.getRadius() + dir.getX() > aiBat.getXLT())){
                    let delta = aiBat.getXLT()-ball.getX()-ball.getRadius();
                    let yDir = yDir * delta / dir.getX();
                    if (yDir < 0){
                        if(-yDir > (ball.getY() - ball.getRadius())){
                            let yExp = 2 * ball.getRadius() - ball.getY() - yDir;
                        }
                        else{
                            let yExp = ball.getY() + yDir;
                        }
                    }
                    else{
                        if (yDir > (255 - ball.getY() - ball.getRadius())){
                            let yExp = - 2 * ball.getRadius() + 510 - yDir - ball.getY();
                        }
                        else{
                            let yExp = ball.getY() + yDir;
                        }
                    }
                    if ((yExp + ball.getRadius() > aiBat.getYLT())&
                        (yExp - ball.getRadius() < aiBat.getYRB())){
                        do ball.hitBat(aiBat, aiKey);
                    }
                    else{
                        let endGame = true;
                    }
                }
                else{
                    do ball.moveX();
                }
            }
            
            do ball.moveY();
            do Screen.setColor(false);
            do Screen.drawCircle(prevBallX, prevBallY, prevBallRadius);
            do ball.draw();
            

            let aiKey = 0;
            if((ball.getY() < aiBat.getYLT()) | (ball.getY() > aiBat.getYRB()))
            {
                if(ball.getY() < aiBat.getYLT())
                {
                    let aiKey = 131;
                }
                else
                {
                    let aiKey = 133;
                }

                let prevBatXLT = aiBat.getXLT();
                let prevBatYLT = aiBat.getYLT();
                let prevBatXRB = aiBat.getXRB();
                let prevBatYRB = aiBat.getYRB();
                do aiBat.changePosition(aiKey);
                do Screen.setColor(false);
                do Screen.drawRectangle(prevBatXLT, prevBatYLT, prevBatXRB, prevBatYRB);                
                do aiBat.draw();
            }
        } 
        return; 
    }

}
class Bat{
    field int xLT, yLT;
    field int length;
    field int width;
    field int speed;

    constructor Bat new(int x, int incomingSpeed)
    {
        let xLT = x;
        let yLT = 78;
        let length = 100;
        let width = 10;
        let speed = incomingSpeed;
        return this;
    }

    method int getXLT(){
        return xLT;
    }

    method int getYLT(){
        return yLT;
    }

    method int getXRB(){
        return xLT + width - 1;
    }

    method int getYRB(){
        return yLT + length - 1;
    }

    method void changePosition(char key)
    {
        var int dir;
        if (key = 131) {
            let dir = -1;
        }
        else {
            if (key = 133) {
                let dir = 1;
            }
            else {
                return;
            }
        }
        let yLT = Math.min(Math.max(speed * dir + yLT, 0), 256-length);
        return;
    }

    method void draw()
    {
        do Screen.setColor(true);
        do Screen.drawRectangle(xLT, yLT, xLT + width - 1, yLT + length - 1);
        return;
    }

    method void erase()
    {
        do Screen.setColor(false);
        do Screen.drawRectangle(xLT, yLT, xLT + width - 1, yLT + length - 1);
        return;
    }

    method void dispose(){
        do Memory.deAlloc(this);
        return;
    }
}
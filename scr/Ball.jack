class Ball{
    field int x, y, speed;
    field Vector2 direction;
    field int radius;

    constructor Ball new()
    {
        let x = 255;
        let y = 127;
        let direction = Vector2.new(-1,1);
        let speed = 1;
        let radius = 15;
        return this;
    }

    method int getX() 
    {
        return x;
    }

    method int getY()
    {
        return y;
    }

    method int getRadius()
    {
        return radius;
    }

    method Vector2 getDirection(){
        return direction;
    }

    method void changeDirection(Vector2 a)
    {
        let direction = direction.sumVectors(a, direction);
        return;
    }

    method void moveY()
    {
        if (direction.getY() < 0){
            if (-direction.getY() > (y - radius)){
                let y = 2 * radius - direction.getY() - y;
                do direction.reflectY();
            }
            else{
                let y = y + direction.getY();
            }
        }
        else{
            if(direction.getY() > (255 - y - radius)){
                let y = 510 - direction.getY() - y - (2 * radius); 
                do direction.reflectY();
            }
            else{
                let y = y + direction.getY();
            }
        }
        return;
    }

    method void moveX()
    {
        let x = x + direction.getX();
        return;
    }

    method void hitBat(Bat bat, char dirKey)
    {
        var Vector2 hit;
        var Vector2 dir;
        var Vector2 temp;
        do direction.reflectX();
        if (x < bat.getXLT()){
            let x = 2 * bat.getXLT() + direction.getX() - radius - radius - x;
            let hit = Vector2.new(-1,0);
        }
        else{
            let x = 2 * bat.getXRB() + direction.getX() + radius + radius - x;
            let hit = Vector2.new(1,0);
        }
        let dir = Vector2.new(0,0);
        if (dirKey = 131){
            let dir = Vector2.new(0,-1);
        }
        else{
            if (dirKey = 133){
                let dir = Vector2.new(0,1);
            }
        }
        let temp = hit.sumVectors(hit, dir);
        let direction = direction.sumVectors(direction, temp);
        return;
    }

    method void draw()
    {
        do Screen.setColor(true);
        do Screen.drawCircle(x, y, radius);
        return;
    }

    method void erase()
    {
        do Screen.setColor(false);
        do Screen.drawCircle(x, y, radius);
        return;
    }

    method void dispose(){
        do Memory.deAlloc(this);
        return;
    }
}